plugins {
    id 'io.franzbecker.gradle-lombok' version '1.6'
    id 'java'
    id 'maven'
}

def BUILD_NUMBER = System.getenv("BUILD_NUMBER") ?: "0"
def MAVEN_REPO = System.getenv("MAVEN_REPO") ?: ""
def MAVEN_USERNAME = System.getenv("MAVEN_USERNAME") ?: ""
def MAVEN_PASSWORD = System.getenv("MAVEN_PASSWORD") ?: ""

archivesBaseName = "JavaTMI"
group = "net.blay09.javatmi"
version = "0.1." + BUILD_NUMBER

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    provided
    provided.extendsFrom(compile)
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

repositories {
    mavenCentral()
    maven {
        url 'http://repo.blay09.net'
    }
}

dependencies {
    compile 'net.blay09:javairc:0.0.9'
    provided 'org.projectlombok:lombok:1.16.6'
    testCompile 'junit:junit:4.12'
}

task delombok(type: io.franzbecker.gradle.lombok.task.DelombokTask) {
    args("src/main/java", "-d", "src/main/generated")
}
jar.dependsOn delombok

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO) {
                authentication(userName: MAVEN_USERNAME, password: MAVEN_PASSWORD)
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
            }

            pom.withXml {
                asNode().dependencies.dependency.findAll {
                    it.scope.text() == "compile"
                }.each {
                    if(it.optional)
                        it.optional.value = 'true'
                    else
                        it.appendNode('optional', 'true');
                }
            }
        }
    }
}
